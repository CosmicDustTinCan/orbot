apply from: "../commons-device.gradle"
apply plugin: 'kotlin-android'
apply from : '../dependencies.gradle'

/* gets the version name from the latest Git tag, stripping the leading v off */
def getVersionName = { ->
    def stdout = new ByteArrayOutputStream()
    exec {
        commandLine 'git', 'describe', '--tags', '--always'
        standardOutput = stdout
    }
    return stdout.toString().trim()
}

android {
    defaultConfig {
        applicationId "org.torproject.android"
        versionName getVersionName()
        minSdkVersion 21
        compileSdkVersion 31
        targetSdkVersion 31
        multiDexEnabled true
    }
    compileOptions {
        coreLibraryDesugaringEnabled true
    }
    lintOptions {
        htmlReport true
        xmlReport false
        textReport false
        lintConfig file("../lint.xml")
        disable "InvalidPackage"
    }
    buildTypes {
        release {
            shrinkResources false
            minifyEnabled false
        }
    }

    flavorDimensions "free"

    androidResources {
        noCompress 'tflite'
    }

    productFlavors {
        fullperm {
            dimension "free"

            minSdkVersion 21
            applicationId 'org.torproject.android'
            targetSdkVersion 31
            //versionCode 1662300400
            versionCode 1700100100

            versionName getVersionName()
            archivesBaseName = "Orbot-$versionName"
        }
    }

    splits {
        abi {
            universalApk true
        }
    }

    dependencies {
        implementation(
                project(':appcore'),
                project(':intentintegrator'),
                project(':orbotservice'),
                libs.android_material,
                libs.android_volley,
                libs.androidx_localbroadcast,
                libs.ipt_proxy
        )
        implementation "com.squareup.retrofit2:retrofit:2.9.0"
        implementation 'com.squareup.retrofit2:converter-gson:2.9.0'

        androidTestImplementation libs.fastlane_screengrab
        coreLibraryDesugaring 'com.android.tools:desugar_jdk_libs:1.1.5'

        //building from AAR built from https://github.com/guardianproject/tor-android/commit/95eca2a8c9eb44068f11332d7731580a13ef7b28
        //    until a new full release is done
       implementation files('../libs/tor-android-binary-release.aar')

        //experimental machine learning

        // Tensorflow lite dependencies
        implementation 'org.tensorflow:tensorflow-lite-task-text:0.4.0'
        implementation 'org.tensorflow:tensorflow-lite-gpu-delegate-plugin:0.4.0'
        implementation 'org.tensorflow:tensorflow-lite-gpu:2.9.0'

        //clean insights for sending labelled data
        implementation 'org.cleaninsights.sdk:clean-insights-sdk:2.5.2'

        //add bottom sheet for consent and measurement
        implementation 'com.maxkeppeler.sheets:core:2.2.6'
        implementation 'com.maxkeppeler.sheets:input:2.2.6'
        implementation 'com.maxkeppeler.sheets:info:2.2.6'
    }
}


// import DownloadModels task
project.ext.ASSET_DIR = projectDir.toString() + '/src/main/assets'
project.ext.TEST_ASSET_DIR = projectDir.toString() + '/src/androidTest/assets'

apply from:'download_model.gradle'

dependencies {
    implementation "androidx.core:core-ktx:1.8.0"
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk7:$kotlin_version"
}
repositories {
    mavenCentral()
}
